---
title: 汇编语言入门
date: 2021-02-26 17:02:53
tags: Compiler
---

我们常常看到 32位 CPU、64位 CPU 这样的名称，其实指的就是寄存器的大小。32 位 CPU 的寄存器大小就是4个字节。

寄存器只能存放很少量的数据，大多数时候，CPU 要指挥寄存器，直接跟内存交换数据。



### Intel的指令：



[汇编 is so easy](https://github.com/cch123/asmshare/blob/master/layout.md)

```go
mov rax, 0x1 // rax = 1 (把一个16进制的1值赋给rax)
add rax, 11  // rax = rax + 11 （将rax寄存器中的值+11）
sub rax,1    // rax = rax - 1

(带0x的是16进制，像11是十进制)

// 在寄存器之间搬数据
mov rax, rbx // rax = rbx  （用rbx的值，去覆盖rax的值）

// 在寄存器和内存之间搬数据
mov rax, [rbx] // 把 rbx 寄存器的值看作一个地址，把该物理地址的值取出，然后赋值给 rax，即 rax = *rbx

// 在内存之间搬数据
- 你想多了，不可以的

push rax => 将 rax 的值存储到栈顶，并将 rsp 上移 8 个字节
pop rax => 将 rsp 指向的内存位置的 8 个字节移动到 rax，并将 rsp 从栈顶下移 8 个字节

jmp addr => 跳转到指定地址
```


<br>

函数 or 过程


函数调用在汇编上的本质：

```go
jmp 跳进去
jmp 跳回来

```
cpu 很傻，跳回来的时候我必须到当前过程的下一条指令位置，难道我还需要在被调用过程里知道调用者的过程下一条指令位置？

```go
call => push pc; jmp to callee addr;
ret => pop pc;
```

所以高级编程语言函数调用的基础


要点，在栈上记录下一条指令位置，即我们常说的 return address，理解成路标就行。在执行完被调用函数的时候，顺着路标回去继续向下走。




Intel CPU上栈是从高地址向低地址增长的

rbp：函数栈的栈底

rsp：栈的栈顶




16个通用寄存器



```go
rax = 0x0000000001053940
       rbx = 0x0000000000000000
       rcx = 0x000000c000000300
       rdx = 0x00000000010750c0
       rdi = 0x0000000000000000
       rsi = 0x0000000000000001
       rbp = 0x000000c0000427d0
       rsp = 0x000000c000042790
        r8 = 0x7fffffffffffffff
        r9 = 0xffffffffffffffff
       r10 = 0x00000000010c0478
       r11 = 0x0000000000000202
       r12 = 0x0000000000203000
       r13 = 0x0000000000000000
       r14 = 0x0000000000000178
       r15 = 0x0000000000000004
       rip = 0x000000000105394f
```

前6个主要用于参数传递


rip存指令地址






对于Plan9 汇编，寄存器有如下差别：


```go
rax -> AX
rbx -> BX
r8  -> R8
```

<br>

### 使用汇编知识

<br>

在plan9中，常数要用$开头，表示数字





可以使用objdump查看intel指令的反汇编结果




但是如果有 NOSPLIT 这个 flag，则可以忽略参数和返回值占用的空间，就像上述这个例子，只有一个$8 。表示 frame size 只有 8 字节大小。这从汇编中也能看出来 MOVQ CX, 0(SP) ,因为 MOVQ 表示这个操作的操作对象是 8 字节的

MOV 指令有有好几种后缀 MOVB MOVW MOVL MOVQ 分别对应的是 1 字节 、2 字节 、4 字节、8 字节




[Plan 9 汇编语言](https://golang.design/under-the-hood/zh-cn/part1basic/ch01basic/asm/)


伪寄存器
伪寄存器不是真正的寄存器，而是由工具链维护的虚拟寄存器，例如帧指针。

FP, Frame Pointer：帧指针，参数和本地 PC, Program Counter: 程序计数器，跳转和分支 SB, Static Base: 静态基指针, 全局符号 SP, Stack Pointer: 当前栈帧开始的地方

所有用户定义的符号都作为偏移量写入伪寄存器 FP 和 SB。

汇编代码中需要表示用户定义的符号(变量)时，可以通过 SP 与偏移还有变量名的组合，比如x-8(SP) ，因为 SP 指向的是栈顶，所以偏移值都是负的，x则表示变量名





SB, Static Base: 静态基地址寄存器



NOSPLIT和go://nosplit一样，确信栈空间够用，不用再检查是否需要栈扩充了~

汇编文件.s与 .go在一个文件夹下即可，不用名称相同. 使用`go build`就能构建出来


如 

#### 例1： 实现一个简单的 a+b

a.go

```go
package main

import "fmt"

func add(a, b int) int // 汇编函数声明

func main() {
    fmt.Println(add(10, 11))
}
```

```go
#include "textflag.h"

// func add(a, b int) int
TEXT ·add(SB), NOSPLIT, $0-24
    MOVQ a+0(FP), AX // 参数 a
    MOVQ b+8(FP), BX // 参数 b
    ADDQ BX, AX    // AX += BX
    MOVQ AX, ret+16(FP) // 返回
    RET
```

"你能解释这里为什么是 $0-24 吗？"




```go
TEXT ·IndexString(SB),NOSPLIT,$0-40
	MOVQ a_base+0(FP), DI
	MOVQ a_len+8(FP), DX
	MOVQ b_base+16(FP), R8
	MOVQ b_len+24(FP), AX
	MOVQ DI, R10
	LEAQ ret+32(FP), R11
	JMP  indexbody<>(SB)  // 无条件跳转 之 跳转到标签，可以跳转到同一函数内的标签位置，https://segmentfault.com/a/1190000039978109， https://github.com/golang/arch/blob/master/x86/x86.csv
```




挨个拆解：

函数的声明以`TEXT`标识开头，以`${package}·${function}`为函数名。 如果函数属于本package，通常可以不写`${package}`，只留`·${function}`即可。

(为什么要叫 TEXT ？如果对程序数据在文件中和内存中的分段稍有了解的同学应该知道，我们的代码在二进制文件中，是存储在 .text 段中的，这里也就是一种约定俗成的起名方式)

`$0`表示该函数栈大小为0byte，计算栈大小时，需要考虑局部变量和本函数内调用其他函数时，需要传参的空间，不含函数返回地址和`CALLER BP`。 `$40`表示该函数*入参*和*返回值*一共有40byte。当有NOSPLIT标识时，可以不写输入参数、返回值占用的大小。


`func IndexString(a, b string) int`入参为两个string，返回值为int。  因为string占16个byte，int占8个字节(int占8个，int32占4个，int64占8个)，可参考 [利用反射,探究Go语言中的数据类型](https://dashen.tech/2020/06/27/%E5%8F%8D%E5%B0%84/),故而是`$40`


<br>


全局变量声明


`<>`表示该变量只在该文件内全局可见



> 地址运算也是用 lea 指令，英文原意为Load Effective Address，amd64 平台地址都是8个字节，所以直接就用LEAQ就好：



`index_amd64.s`

```sql
AX: length of string, that we are searching for
DX: length of string, in which we are searching
DI: pointer to string, in which we are searching
R8: pointer to string, that we are searching for
R11: address, where to put return value
Note: We want len in DX and AX, because PCMPESTRI implicitly consumes them

AX：我们要搜索的字符串长度
DX：字符串的长度，我们在其中搜索
DI：指向字符串的指针，我们在其中搜索
R8：指向字符串的指针，我们正在搜索
R11：地址，返回值放在哪里
注意：我们需要 DX 和 AX 中的 len，因为 PCMPESTRI 隐式消耗它们

```

```go
// AX: length of string, that we are searching for
// DX: length of string, in which we are searching
// DI: pointer to string, in which we are searching
// R8: pointer to string, that we are searching for
// R11: address, where to put return value
// Note: We want len in DX and AX, because PCMPESTRI implicitly consumes them
TEXT indexbody<>(SB),NOSPLIT,$0
	CMPQ AX, DX
	JA fail
	CMPQ DX, $16
	JAE sse42
no_sse42:  //SSE 4.2为Intel指令集，https://zh.wikipedia.org/wiki/SSE4，  https://baike.baidu.com/item/SSE%204.2/138412
	CMPQ AX, $2
	JA   _3_or_more
	MOVW (R8), R8
	LEAQ -1(DI)(DX*1), DX
loop2:
	MOVW (DI), SI
	CMPW SI,R8
	JZ success
	ADDQ $1,DI
	CMPQ DI,DX
	JB loop2
	JMP fail
_3_or_more:
	CMPQ AX, $3
	JA   _4_or_more
	MOVW 1(R8), BX
	MOVW (R8), R8
	LEAQ -2(DI)(DX*1), DX
loop3:
	MOVW (DI), SI
	CMPW SI,R8
	JZ   partial_success3
	ADDQ $1,DI
	CMPQ DI,DX
	JB loop3
	JMP fail
partial_success3:
	MOVW 1(DI), SI
	CMPW SI,BX
	JZ success // 有条件跳转 JZ target, 如果 zero flag 被 set 过，则跳转
	ADDQ $1,DI
	CMPQ DI,DX
	JB loop3
	JMP fail
_4_or_more:
	CMPQ AX, $4
	JA   _5_or_more
	MOVL (R8), R8
	LEAQ -3(DI)(DX*1), DX
loop4:
	MOVL (DI), SI
	CMPL SI,R8
	JZ   success
	ADDQ $1,DI
	CMPQ DI,DX
	JB loop4
	JMP fail
_5_or_more:
	CMPQ AX, $7
	JA   _8_or_more
	LEAQ 1(DI)(DX*1), DX
	SUBQ AX, DX
	MOVL -4(R8)(AX*1), BX
	MOVL (R8), R8
loop5to7:
	MOVL (DI), SI
	CMPL SI,R8
	JZ   partial_success5to7
	ADDQ $1,DI
	CMPQ DI,DX
	JB loop5to7
	JMP fail
partial_success5to7:
	MOVL -4(AX)(DI*1), SI
	CMPL SI,BX
	JZ success
	ADDQ $1,DI
	CMPQ DI,DX
	JB loop5to7
	JMP fail
_8_or_more:
	CMPQ AX, $8
	JA   _9_or_more
	MOVQ (R8), R8
	LEAQ -7(DI)(DX*1), DX
loop8:
	MOVQ (DI), SI
	CMPQ SI,R8
	JZ   success
	ADDQ $1,DI
	CMPQ DI,DX
	JB loop8
	JMP fail
_9_or_more:
	CMPQ AX, $15
	JA   _16_or_more
	LEAQ 1(DI)(DX*1), DX
	SUBQ AX, DX
	MOVQ -8(R8)(AX*1), BX
	MOVQ (R8), R8
loop9to15:
	MOVQ (DI), SI
	CMPQ SI,R8
	JZ   partial_success9to15
	ADDQ $1,DI
	CMPQ DI,DX
	JB loop9to15
	JMP fail
partial_success9to15:
	MOVQ -8(AX)(DI*1), SI
	CMPQ SI,BX
	JZ success
	ADDQ $1,DI
	CMPQ DI,DX
	JB loop9to15
	JMP fail
_16_or_more:
	CMPQ AX, $16
	JA   _17_or_more
	MOVOU (R8), X1
	LEAQ -15(DI)(DX*1), DX
loop16:
	MOVOU (DI), X2
	PCMPEQB X1, X2
	PMOVMSKB X2, SI
	CMPQ  SI, $0xffff
	JE   success
	ADDQ $1,DI
	CMPQ DI,DX
	JB loop16
	JMP fail
_17_or_more:
	CMPQ AX, $31
	JA   _32_or_more
	LEAQ 1(DI)(DX*1), DX
	SUBQ AX, DX
	MOVOU -16(R8)(AX*1), X0
	MOVOU (R8), X1
loop17to31:
	MOVOU (DI), X2
	PCMPEQB X1,X2
	PMOVMSKB X2, SI
	CMPQ  SI, $0xffff
	JE   partial_success17to31
	ADDQ $1,DI
	CMPQ DI,DX
	JB loop17to31
	JMP fail
partial_success17to31:
	MOVOU -16(AX)(DI*1), X3
	PCMPEQB X0, X3
	PMOVMSKB X3, SI
	CMPQ  SI, $0xffff
	JE success
	ADDQ $1,DI
	CMPQ DI,DX
	JB loop17to31
	JMP fail
// We can get here only when AVX2 is enabled and cutoff for indexShortStr is set to 63
// So no need to check cpuid
_32_or_more:
	CMPQ AX, $32
	JA   _33_to_63
	VMOVDQU (R8), Y1
	LEAQ -31(DI)(DX*1), DX
loop32:
	VMOVDQU (DI), Y2
	VPCMPEQB Y1, Y2, Y3
	VPMOVMSKB Y3, SI
	CMPL  SI, $0xffffffff
	JE   success_avx2
	ADDQ $1,DI
	CMPQ DI,DX
	JB loop32
	JMP fail_avx2
_33_to_63:
	LEAQ 1(DI)(DX*1), DX
	SUBQ AX, DX
	VMOVDQU -32(R8)(AX*1), Y0
	VMOVDQU (R8), Y1
loop33to63:
	VMOVDQU (DI), Y2
	VPCMPEQB Y1, Y2, Y3
	VPMOVMSKB Y3, SI
	CMPL  SI, $0xffffffff
	JE   partial_success33to63
	ADDQ $1,DI
	CMPQ DI,DX
	JB loop33to63
	JMP fail_avx2
partial_success33to63:
	VMOVDQU -32(AX)(DI*1), Y3
	VPCMPEQB Y0, Y3, Y4
	VPMOVMSKB Y4, SI
	CMPL  SI, $0xffffffff
	JE success_avx2
	ADDQ $1,DI
	CMPQ DI,DX
	JB loop33to63
fail_avx2:
	VZEROUPPER
fail:
	MOVQ $-1, (R11)
	RET
success_avx2:
	VZEROUPPER
	JMP success
sse42:
	CMPB internal∕cpu·X86+const_offsetX86HasSSE42(SB), $1
	JNE no_sse42
	CMPQ AX, $12
	// PCMPESTRI is slower than normal compare,
	// so using it makes sense only if we advance 4+ bytes per compare
	// This value was determined experimentally and is the ~same
	// on Nehalem (first with SSE42) and Haswell.
	JAE _9_or_more
	LEAQ 16(R8), SI
	TESTW $0xff0, SI
	JEQ no_sse42
	MOVOU (R8), X1
	LEAQ -15(DI)(DX*1), SI
	MOVQ $16, R9
	SUBQ AX, R9 // We advance by 16-len(sep) each iteration, so precalculate it into R9
loop_sse42:
	// 0x0c means: unsigned byte compare (bits 0,1 are 00)
	// for equality (bits 2,3 are 11)
	// result is not masked or inverted (bits 4,5 are 00)
	// and corresponds to first matching byte (bit 6 is 0)
	PCMPESTRI $0x0c, (DI), X1
	// CX == 16 means no match,
	// CX > R9 means partial match at the end of the string,
	// otherwise sep is at offset CX from X1 start
	CMPQ CX, R9
	JBE sse42_success
	ADDQ R9, DI
	CMPQ DI, SI
	JB loop_sse42
	PCMPESTRI $0x0c, -1(SI), X1
	CMPQ CX, R9
	JA fail
	LEAQ -1(SI), DI
sse42_success:
	ADDQ CX, DI
success:
	SUBQ R10, DI
	MOVQ DI, (R11)
	RET
```





```go
// 无条件跳转
JMP addr   // 跳转到地址，地址可为代码中的地址，不过实际上手写不会出现这种东西
JMP label  // 跳转到标签，可以跳转到同一函数内的标签位置
JMP 2(PC)  // 以当前指令为基础，向前/后跳转 x 行
JMP -2(PC) // 同上

// 有条件跳转
JZ target // 如果 zero flag 被 set 过，则跳转
```






[肝了一上午的Golang之Plan9入门](https://zhuanlan.zhihu.com/p/348227592?utm_source=wechat_timeline&utm_medium=social&utm_oi=608411979623305216)
[golang 汇编](https://lrita.github.io/2017/12/12/golang-asm/#%E5%9C%B0%E5%9D%80%E8%BF%90%E7%AE%97)
[A Quick Guide to Go's Assembler](https://golang.org/doc/asm)
[go/build](https://pkg.go.dev/go/build)
[学习 Golang 的 Plan 9 汇编](http://www.shiguopeng.cn/archives/503)
[plan9 assembly 完全解析](https://segmentfault.com/a/1190000039978109)
[golang/arch](https://github.com/golang/arch/blob/master/x86/x86.csv)

[相关图片](https://www.google.com/search?newwindow=1&sxsrf=AOaemvIRN2Sg1yNYCzrjG6m7kEDDRwm3fw:1631694339084&source=univ&tbm=isch&q=TEXT+indexbody%3C%3E(SB),NOSPLIT,$0+CMPQ+AX,+DX+JA+f&sa=X&ved=2ahUKEwjWztmLx4DzAhWhwjgGHRIZAc0QjJkEegQIIhAC&biw=1775&bih=1218)


[golang内核系列--深入理解plan9汇编&实践](https://zhuanlan.zhihu.com/p/56750445)

[cch123/asmshare](https://github.com/cch123/asmshare/blob/master/layout.md)

---

[Go语言高级编程](https://www.bookstack.cn/read/advanced-go-programming-book/ch3-asm-ch3-04-func.md)


[GO汇编-函数](https://www.cnblogs.com/binHome/p/13034103.html)

[golang内核系列--深入理解plan9汇编&实践](https://blog.csdn.net/weixin_39976382/article/details/110851768)

[汇编语言入门教程](http://www.ruanyifeng.com/blog/2018/01/assembly-language-primer.html)

[图文讲解：Go 中的循环是如何转为汇编的？](https://segmentfault.com/a/1190000040037915)

[曹春晖：谈一谈 Go 和 Syscall](http://blog.itpub.net/69908606/viewspace-2642153/)

[[译]go 和 plan9 汇编](https://xargin.com/go-and-plan9-asm/)



[指示叶子函数不进行栈分裂](https://www.google.com/search?q=%E6%8C%87%E7%A4%BA%E5%8F%B6%E5%AD%90%E5%87%BD%E6%95%B0%E4%B8%8D%E8%BF%9B%E8%A1%8C%E6%A0%88%E5%88%86%E8%A3%82&oq=%E6%8C%87%E7%A4%BA%E5%8F%B6%E5%AD%90%E5%87%BD%E6%95%B0%E4%B8%8D%E8%BF%9B%E8%A1%8C%E6%A0%88%E5%88%86%E8%A3%82&aqs=chrome..69i57.121j0j1&sourceid=chrome&ie=UTF-8)